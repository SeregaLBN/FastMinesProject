apply plugin: 'com.android.application'

gradle.taskGraph.beforeTask { Task task ->
    if ((task.project.name == 'FastMines_android') && (task.name == 'preAppReleaseBuild')) {
        if (System.env.FAST_MINES_ANDROID_KEYSTORE == null)
            System.err.println('The system environment variable FAST_MINES_ANDROID_KEYSTORE must be defined')
        if (System.env.FAST_MINES_ANDROID_STORE_PASS == null)
            System.err.println('The system environment variable FAST_MINES_ANDROID_STORE_PASS must be defined')
        if (System.env.FAST_MINES_ANDROID_KEY_ALIAS == null)
            System.err.println('The system environment variable FAST_MINES_ANDROID_KEY_ALIAS must be defined')
        if (System.env.FAST_MINES_ANDROID_KEY_PASS == null)
            System.err.println('The system environment variable FAST_MINES_ANDROID_KEY_PASS must be defined')
    }
}

android {
    signingConfigs {
        def keyStore = System.env.FAST_MINES_ANDROID_KEYSTORE
        if (keyStore == null)
            keyStore = 'The system environment variable FAST_MINES_ANDROID_KEYSTORE must be defined'
        release {
            storeFile     file(keyStore)
            storePassword System.env.FAST_MINES_ANDROID_STORE_PASS
            keyAlias      System.env.FAST_MINES_ANDROID_KEY_ALIAS
            keyPassword   System.env.FAST_MINES_ANDROID_KEY_PASS
        }
    }
    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    compileSdkVersion 34

    defaultConfig {
        applicationId 'seregalbn.fast_mines_game.android.app' // otherwise eq AndroidManifest.xml: <manifest ... package="fmg.android.app" />
        minSdkVersion 26
        targetSdkVersion 34
        versionCode 7       // Internal version number. Increase with each new release build
                            //   equals AndroidManifest.xml: <manifest ... android:versionCode=".." />
        versionName '1.0.2' // Version number for users. Doesn't affect anything
                            //   equals AndroidManifest.xml: <manifest ... android:versionName=".." />
        buildConfigField 'boolean', 'DRAW_MODE_FULL', 'true'
        buildConfigField 'boolean', 'DEBUG_OUTPUT'  , 'false'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        androidTest.java.srcDirs += '../src/test_core'
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources true
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            debuggable true
            minifyEnabled false
            buildConfigField 'boolean', 'DRAW_MODE_FULL', 'true'
            buildConfigField 'boolean', 'DEBUG_OUTPUT'  , 'true'
        }
    }
    buildFeatures {
        dataBinding = true
    }

    flavorDimensions 'runMode'
    productFlavors {
        app { // full mode
            dimension 'runMode'
        }
        demo { // demonstration of drawing performance
            dimension 'runMode'
            applicationIdSuffix '.demo'
        }
    }
}


dependencies {
    implementation project(':FastMines_core')

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation 'android.arch.lifecycle:runtime:1.1.1'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
//    androidTestImplementation "androidx.test.runner.AndroidJUnitRunner"
    // Optional -- UI testing with UI Automator
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
//    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
//    androidTestImplementation 'com.android.support.test:rules:1.0.2'
//    // Optional -- Hamcrest library
//    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
//    // Optional -- UI testing with Espresso
//    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
}
