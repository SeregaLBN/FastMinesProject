apply plugin: 'com.android.application'

android {
    signingConfigs {
        release {
            storeFile file(System.getenv("FAST_MINES_ANDROID_KEYSTORE"))
            storePassword  System.getenv("FAST_MINES_ANDROID_STORE_PASS")
            keyAlias       System.getenv("FAST_MINES_ANDROID_KEY_ALIAS")
            keyPassword    System.getenv("FAST_MINES_ANDROID_KEY_PASS")
        }
    }
    compileOptions {
        encoding = 'UTF-8'
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    compileSdkVersion 30

    defaultConfig {
        applicationId 'SeregaLBN.FastMinesGame.Android.App'
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1 // Internal version number. Increase with each new release build
        versionName '1.0.0' // Version number for users. Doesn't affect anything
        buildConfigField 'boolean', 'DRAW_MODE_FULL', 'true'
        buildConfigField 'boolean', 'DEBUG_OUTPUT'  , 'false'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        androidTest.java.srcDirs += '../src/test_core'
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources true
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            debuggable true
            minifyEnabled false
            buildConfigField 'boolean', 'DRAW_MODE_FULL', 'true'
            buildConfigField 'boolean', 'DEBUG_OUTPUT'  , 'true'
        }
    }
    buildFeatures {
        dataBinding = true
    }

    flavorDimensions 'runMode'
    productFlavors {
        app { // full mode
            dimension 'runMode'
        }
        demo { // demonstration of drawing performance
            dimension 'runMode'
            applicationIdSuffix '.demo'
        }
    }
}


dependencies {
    implementation project(':FastMines_core')

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'
    implementation 'android.arch.lifecycle:runtime:1.1.1'
    implementation 'android.arch.lifecycle:extensions:1.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
//    androidTestImplementation "androidx.test.runner.AndroidJUnitRunner"
    // Optional -- UI testing with UI Automator
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
//    androidTestImplementation 'com.android.support:support-annotations:27.1.1'
//    androidTestImplementation 'com.android.support.test:rules:1.0.2'
//    // Optional -- Hamcrest library
//    androidTestImplementation 'org.hamcrest:hamcrest-library:1.3'
//    // Optional -- UI testing with Espresso
//    androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
}
